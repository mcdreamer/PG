cmake_minimum_required(VERSION 3.0)

set(PG_CMAKE_DIR ${CMAKE_CURRENT_LIST_DIR})
set(SFML_DIR ${PG_CMAKE_DIR}/../internal/sfml)

function(pg_set_app_link_libs)

set(BASE_APP_LINK_LIBS PG pile snappy)

if (APPLE)

set(SFMLAUDIO "${SFML_DIR}/Frameworks/sfml-audio.framework")
set(SFMLSYSTEM "${SFML_DIR}/Frameworks/sfml-system.framework")
set(SFMLGRAPHICS "${SFML_DIR}/Frameworks/sfml-graphics.framework")
set(SFMLWINDOW "${SFML_DIR}/Frameworks/sfml-window.framework")

set(PLATFORM_LIBS "objc -framework Foundation -framework Cocoa")

endif()

if (WIN32)

set(SFMLAUDIO "${SFML_DIR}/lib/sfml-audio.lib")
set(SFMLSYSTEM "${SFML_DIR}/lib/sfml-system.lib")
set(SFMLGRAPHICS "${SFML_DIR}/lib/sfml-graphics.lib")
set(SFMLWINDOW "${SFML_DIR}/lib/sfml-window.lib")

file(COPY "${SFML_DIR}/bin/sfml-audio-2.dll" DESTINATION "${CMAKE_BINARY_DIR}")
file(COPY "${SFML_DIR}/bin/sfml-system-2.dll" DESTINATION "${CMAKE_BINARY_DIR}")
file(COPY "${SFML_DIR}/bin/sfml-graphics-2.dll" DESTINATION "${CMAKE_BINARY_DIR}")
file(COPY "${SFML_DIR}/bin/sfml-window-2.dll" DESTINATION "${CMAKE_BINARY_DIR}")
file(COPY "${SFML_DIR}/openal32.dll" DESTINATION "${CMAKE_BINARY_DIR}")

endif()

set(INTERNAL_LINK_LIBS ${SFMLAUDIO} ${SFMLSYSTEM} ${SFMLGRAPHICS} ${SFMLWINDOW} PARENT_SCOPE)
set(APP_LINK_LIBS ${BASE_APP_LINK_LIBS} ${PLATFORM_LIBS} ${SFMLAUDIO} ${SFMLSYSTEM} ${SFMLGRAPHICS} ${SFMLWINDOW} PARENT_SCOPE)

endfunction()


function(pg_setup)

set(JSON_CPP "${PG_CMAKE_DIR}/../thirdparty/jsoncpp/dist/jsoncpp.cpp")

execute_process(
  WORKING_DIRECTORY "${PG_CMAKE_DIR}/../thirdparty/jsoncpp/"
  COMMAND python amalgamate.py
)

set(INTERNAL_CPP ${JSON_CPP} PARENT_SCOPE)

endfunction()


function(build_pg_app SRCS RESOURCES_DIR RESOURCES_JSON APP_NAME APP_ID INFO_PLIST)

set(STOCKPILE_DIR "${PG_CMAKE_DIR}/../thirdparty/stockpile/stockpile")

set(SNAPPY_BUILD_TESTS OFF CACHE BOOL "")

set(CMAKE_BUILD_TYPE RELEASE)
set(CMAKE_CXX_STANDARD 14)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

add_subdirectory(${PG_CMAKE_DIR} "./PG")
add_subdirectory("${STOCKPILE_DIR}/tool" "./stockpiletool")

include("${STOCKPILE_DIR}/build/stockpile_cmake.txt")

set(BOOST_ROOT $ENV{BOOST_DIR})
set(Boost_USE_STATIC_LIBS ON)
set(Boost_USE_MULTITHREADED ON)
find_package(Boost COMPONENTS filesystem system REQUIRED)

include_directories(
  "../"
  ${Boost_INCLUDE_DIRS}
)

set(PILE_OUTPUT resources.pile)

# Xcode generator uses a subdirectory with the config name
set(STOCKPILE_CONFIG_DIR "")
if(${CMAKE_GENERATOR} STREQUAL "Xcode")
set(STOCKPILE_CONFIG_DIR $<CONFIG>)
endif()

build_pile(
  ${stockpile_BINARY_DIR}/${STOCKPILE_CONFIG_DIR}
  ${RESOURCES_DIR}/${RESOURCES_JSON}
  ${RESOURCES_DIR}
  ${PILE_OUTPUT}
)

set_source_files_properties(${PILE_OUTPUT} PROPERTIES MACOSX_PACKAGE_LOCATION Resources)

pg_setup()
pg_set_app_link_libs()

add_executable(${APP_NAME} "${INTERNAL_CPP}" "${SRCS}" ${PILE_OUTPUT})

target_link_libraries(${APP_NAME} ${APP_LINK_LIBS} ${Boost_LIBRARIES})

if(APPLE)

set_target_properties(${APP_NAME} PROPERTIES
  MACOSX_BUNDLE TRUE
  MACOSX_FRAMEWORK_IDENTIFIER ${APP_ID}
  MACOSX_BUNDLE_INFO_PLIST ${INFO_PLIST}
)

if(NOT ${CMAKE_GENERATOR} STREQUAL "Xcode") # Xcode does this for us

set_target_properties(${APP_NAME}
  PROPERTIES BUILD_WITH_INSTALL_RPATH 1
             INSTALL_NAME_DIR "@rpath"
             INSTALL_RPATH "@executable_path/../Frameworks"
)

set(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)

set(OUTPUT_FRAMEWORKS_DIR ${CMAKE_BINARY_DIR}/${APP_NAME}.app/Contents/Frameworks)

add_custom_command(
  TARGET ${APP_NAME}
  POST_BUILD
  COMMAND mkdir -p ${OUTPUT_FRAMEWORKS_DIR}
  COMMAND rsync -ra ${SFML_DIR}/Frameworks/sfml-* ${OUTPUT_FRAMEWORKS_DIR}
)

endif()

endif()

endfunction()